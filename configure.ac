#                                               -*- Autoconf -*-
# Process this file with autoconf to produce a configure script.

AC_PREREQ([2.65])
AC_INIT([tmsmt], [0.0.1-alpha1], [ntd@rice.edu])

AM_INIT_AUTOMAKE([subdir-objects])
m4_ifdef([AM_SILENT_RULES], [AM_SILENT_RULES([yes])])

AC_CONFIG_SRCDIR([lisp/tmsmt.asd])
AC_CONFIG_HEADERS([config.h])
AC_CONFIG_MACRO_DIR([m4])

AC_PROG_CC_C99
LT_INIT

AC_CHECK_PROGS([AARX_CORE], [aarx.core])

AS_IF([test "x$AARX_CORE" = x],
      [AC_MSG_ERROR([Could not found aarx.core.  Is amino installed?])])


# Check for Lex/Flex
AC_ARG_WITH([lex],
            AS_HELP_STRING([--without-lex], [Do not run lex/flex]),
            [],
            [with_lex=yes])
AS_IF([test "x$with_lex" != xyes],
      [AM_PROG_LEX])
AM_CONDITIONAL([HAVE_LEX], [test "x$LEX" != x -a "x$with_lex" = xyes])


# Amino
PKG_CHECK_MODULES([AMINO], [amino],
[
  CFLAGS="$CFLAGS $AMINO_CFLAGS"
  CXXFLAGS="$CXXFLAGS $AMINO_CXXFLAGS"
  LDFLAGS="$LDFLAGS $AMINO_LIBS"
],
[
  AC_MSG_ERROR([Amino is required])
])


# Doxygen
AC_CHECK_PROGS([DOXYGEN],[doxygen])
AM_CONDITIONAL([HAVE_DOXYGEN], [test "x$DOXYGEN" != x])

# Man Pages
AC_ARG_ENABLE([man],
              [AS_HELP_STRING([--disable-man], [Do not build man pages])],
              [],
              [enable_man="yes"])

AS_IF([test "x$enable_man" = xyes -a "x$build" = "x$host"],
      [AC_CHECK_PROGS([HELP2MAN], [help2man])])
AM_CONDITIONAL([HAVE_HELP2MAN], [test x$HELP2MAN != x])


AC_CONFIG_FILES([Makefile Doxyfile])
AC_OUTPUT

AS_BOX([TMSMT CONFIGURATION SUMMARY])
AC_MSG_NOTICE([AARX_CORE:         $AARX_CORE])
AC_MSG_NOTICE([LEX:               $LEX])
AS_BOX([END SUMMARY])
